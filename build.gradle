plugins {
    id 'groovy'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.3'
}

repositories {
    jcenter()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.22'

    compileOnly 'org.projectlombok:lombok:1.16.12'
    testCompileOnly 'org.projectlombok:lombok:1.16.12'

    testCompile 'org.codehaus.groovy:groovy-all:2.4.7'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-3'
    testRuntime 'ch.qos.logback:logback-classic:1.1.8'
    testRuntime 'cglib:cglib-nodep:3.2.4'
}

group = 'org.hidetake'
description = 'Pojofill'
version = System.getenv('CIRCLE_TAG') ?: 'SNAPSHOT'
ext {
    license = 'Apache-2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    vcsUrl = 'https://github.com/int128/pojofill.git'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from "${buildDir}/docs/javadoc"
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    publications {
        release(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifactId project.name
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name project.name
                    description project.description
                    url project.vcsUrl
                    scm {
                        url project.vcsUrl
                        connection "scm:git:$project.vcsUrl"
                    }
                    licenses {
                        license {
                            name project.license
                            url project.licenseUrl
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id project.group
                            name project.group
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER') ?: findProperty('bintrayUser')
    key  = System.getenv('BINTRAY_KEY')  ?: findProperty('bintrayKey')
    publications = ['release']
    publish = true
    pkg {
        repo = 'maven'
        name = project.name
        licenses = [project.license]
        vcsUrl = project.vcsUrl
        version {
            name = project.version
        }
    }
}
